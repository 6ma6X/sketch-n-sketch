;
; Cult of Lambda
;
; Some fun 
;
(let [x0 y0 w h] [0 0 500 500]
(let wstripe (/ w 6!)
(let xoff (+ x0 wstripe)
(let yoff (+ y0 (/ h 4!))
(let minrad
  (if (< (/ wstripe 7.5!) (/ h 30!))
    (/ wstripe 7.5!)
    (/ h 15!))
(let min
  (if (< w h)
    (* 0.4! w)
    (* 0.4! h))
(let rotate (\a (/ (* (+ 1! a) (pi)) 4!))
(let figure 
  (\(x y) 
    (let [x1 x2 x3] (map (\n (+ x (* wstripe n))) [1.2! 1.5! 1.8!])
    (let [y1 y2 y3 y4] (map (\n (+ y (/ (/ h 2!) n))) [4.3! 2.8! 1.9! 1.4!])
    (let figline (\[[a b] [c d]] (line 'orange' (/ minrad 2!) a b c d))
      (snoc
        (ellipse 'orange' x2 y1 (/ wstripe 7.5!) (/ h 30!))
        (map
          figline
            [[[x1 y1] [x1 y2]]
            [[x1 y2] [x3 y2]]
            [[x3 y1] [x3 y2]]
            [[x1 y4] [x1 y3]]
            [[x1 y3] [x3 y3]]
            [[x3 y3] [x3 y4]]
            [[x2 y1] [x2 y3]]]))))))
(let logo
  (\(x y)
    (let [xl yl wl hl delta] [x y w h 27.5]
    (let [xw yh w2 h2] [(+ xl wl) (+ yl hl) (div wl 2) (div hl 2)]
    (let poly (\pts (polygon 'black' 'none' 0 pts))
      [
      (rect 'white' xl yl wl hl)
      (poly
        [[(+ xl delta) yl]
         [xw yl]
         [xw (- yh delta)]])
      (poly
        [[xl (+ yl delta)]
         [xl (- yh delta)]
         [(- (+ xl w2) delta) (+ yl h2)]])
      (poly
        [[(+ xl delta) yh]
         [(- xw delta) yh]
         [(+ xl w2) (+ (+ yl h2) delta)]])
    ]))))
;
  (svg
    (append
      (logo (+ x0 80!) (+ y0 36!))
      (concatMap 
        (\i 
          (figure 
            (+ (* 0.41! w) (+ x0 (* min (cos (rotate i)))))
            (+ (* 0.38! h) (+ y0 (* min (sin (rotate i)))))))
        (range 0! 7!)))))))))))))
